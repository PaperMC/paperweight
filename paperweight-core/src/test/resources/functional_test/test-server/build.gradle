plugins {
    id "java"
    id 'io.papermc.paperweight.core'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://repo.papermc.io/repository/maven-public/"
    }
}

def fake = Boolean.getBoolean("fake")

dependencies {
    implementation project(":test-api")

    if (fake) {
        mache files('../mache.zip') // use fake mache for testing

        // TODO: fake mache does not have dependencies
        // this could be resolved by creating a fake maven repo that holds proper module metadata
        implementation 'com.github.oshi:oshi-core:6.6.5'
    } else {
        mache 'io.papermc:mache:1.21.4+build.3'
    }
}

paperweight {
    softSpoon = true

    if (fake) {
        // use fake mojang data for testing
        minecraftVersion = 'fake'
        minecraftManifestUrl = 'file://project/../fake_mojang/version_manifest.json'

        paper {
            sourcePatchDir.set(file('../fake-patches/sources'))
            resourcePatchDir.set(file('../fake-patches/resources'))
            featurePatchDir.set(file('../fake-patches/features'))

            additionalAts.set(file('../build-data/fake.at'))

            paperServerDir.set(file('./'))
        }
    } else {
        minecraftVersion = '1.21.4'

        paper {
            paperServerDir.set(file('./'))
        }
    }

    serverProject = project(':test-server')
}
